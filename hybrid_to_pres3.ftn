***function hybrid_to_pres  - function to convert from hybrid to pressure

      integer function hybrid_to_pres3(pressure,ptop,ps,NI,NJ,
     %                                                 hyb,etatop,NK)

      implicit none
      integer NI,NJ,NK
      real pressure(NI*NJ,NK),ptop,ps(NI*NJ),hyb(NK),etatop
*
*author
*     C Beaudoin   Aug 2005 from hybrid_to_pres2
*     written by Vivian Lee/Michel Valin   Nov.28, 2001
*
*revision
*
*object
*     To derive pressure fields and model hybrid levels (levels used by
*     the model) from user-defined hybrid levels and the hybrid reference 
*     parameters(ptop,rcoef,pref)
*arguments
* ________________________________________________________________
*  Name        I/O      Description
* ----------------------------------------------------------------
* pressure      O       array of pressure levels (same units as ps)
* ptop          I       average pressure at the top (mb)
* ps            I       pressure at the surface (mb or pascals)
* hyb           I       array of user-defined hybrid levels (0.0 to 1.0)
* etatop        I       eta at top of model (variable E1 in sef output file)
* ----------------------------------------------------------------
* the function will return 0 upon success, -1 if there is an error
**

      integer i,k
      real*8 pibb(nk),pia(nk),eta1
      real*8 conv,fact

      hybrid_to_pres3=-1
      if (ptop .lt.0 .or. ptop .gt.1200) then
         print *,'ERROR in hybrid_to_pres: rcoef must be between 1.0 and 2.0'
         print *,'ERROR in hybrid_to_pres:'
         print *,'ptop  must be a value between 0 and 1200'
      return
      endif

      fact = 1.0
*     detect if ps is in millibars or pascals
      if (ps(1).lt.40000.0) then
          conv = 100.0
          fact = fact/100.0
      else
          conv = 1.0
      endif


      do k=1,nk
         if (hyb(k).lt.0 .or. hyb(k).gt. 1.0) then
            print *,'ERROR in hybrid_to_pres:'
            print *,'invalid value(s) in hybrid coordinate array'
         return
         endif
      enddo

      eta1 = 1./(1.-etatop)
      do k = 1,nk
         pibb(k)  = (hyb(k) - etatop)*eta1 
         pia(k)  = ptop * ( 1. - pibb(k) )
      enddo

      do k=1,nk
         pibb(k) = pibb(k)*conv
         do i=1,ni*nj
            pressure(i,k) = (pia(k)+pibb(k)*ps(i)) * fact
!            if(k.eq.1) write(*,*)  pressure(i,k), pia(k),pibb(k),ps(k),hyb(k)
         enddo
      enddo 
      hybrid_to_pres3=0
      return
      end
