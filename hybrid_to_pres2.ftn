***function hybrid_to_pres2 - function to convert from hybrid to pressure

      integer function hybrid_to_pres2(pressure,hybm,ptop,ps,NI,NJ,rcoef,
     %                                                 pref,hyb,NK)

      implicit none
      integer NI,NJ,NK
      real pressure(NI*NJ,NK),ptop,ps(NI*NJ),rcoef,pref,hyb(NK),hybm(NK)
*
*author
*     Vivian Lee/Michel Valin    Nov.28, 2001
*
*revision
*
*object
*     To derive pressure fields and model hybrid levels (levels used by
*     the model) from user-defined hybrid levels and the hybrid reference 
*     parameters(ptop,rcoef,pref)
*arguments
* ________________________________________________________________
*  Name        I/O      Description
* ----------------------------------------------------------------
* pressure      O       array of pressure levels (same units as ps)
* hybm          O       array of model hybrid levels (0.0 to 1.0)
*                       - calculated using ptop,rcoef and pref
* ptop          I       average pressure at the top (mb)
* ps            I       pressure at the surface (mb or pascals)
* rcoef         I       coefficient (1.0 to 2.0)
* pref          I       reference pressure in mb (normally = 800mb)
* hyb           I       array of user-defined hybrid levels (0.0 to 1.0)
* ----------------------------------------------------------------
* the function will return 0 upon success, -1 if there is an error
**

      integer i,k
      real*8 hybm_8(nk),prpref,pr1,pibb(nk),pia(nk)
      real*8 conv,fact

      hybrid_to_pres2=-1
      if (rcoef.lt.1.0.or.rcoef.gt.2.0) then
          print *,'ERROR in hybrid_to_pres: rcoef must be between 1.0 and 2.0'
          return
      endif
      if (pref .lt.400 .or. pref .gt.1050) then
          print *,'ERROR in hybrid_to_pres: pref must be a value between 400 and 1050'
          return
      endif
      if (ptop .lt.0 .or. ptop .gt.1200) then
          print *,'ERROR in hybrid_to_pres: ptop  must be a value between 0 and 1200'
          return
      endif
      if (abs(rcoef-1.0).lt.1.0e-5) rcoef = 1.0

      fact = 1.0
*     detect if ps is in millibars or pascals
      if (ps(1).lt.40000.0) then
          conv = 100.0
          fact = fact/100.0
      else
          conv = 1.0
      endif


      do k=1,nk
         if (hyb(k).lt.0 .or. hyb(k).gt. 1.0) then
           print *,'ERROR in hybrid_to_pres: invalid value(s) in hybrid coordinate array'
           return
         endif
         hybm_8(k)= hyb(k) + (1-hyb(k)) * ptop/pref
      enddo

      prpref = 100.*pref

      pr1 = 1./(1. - ptop/pref)
      do k = 1,nk
         pibb(k)  = ((hybm_8(k) - ptop/pref)*pr1 ) ** rcoef
         pia(k)  = prpref * ( hybm_8(k) - pibb(k) )
      enddo

      do k=1,nk
         pibb(k) = pibb(k)*conv
         do i=1,ni*nj
            pressure(i,k) = (pia(k)+pibb(k)*ps(i)) * fact
!            if(k.eq.1) write(*,*)  pressure(i,k), pia(k),pibb(k),ps(k),hyb(k)
         enddo
         hybm(k) = hybm_8(k)
      enddo 
      hybrid_to_pres2=0
      return
      end
